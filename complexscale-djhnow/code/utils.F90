#include "definitions.INC"

module UTILMOD
CONTAINS
  
SUBROUTINE WRITE_EIGENVECTORS(PSI,NUM,MPIDATA,M,OUTFILE)
  USE STRUCTS
  IMPLICIT NONE
  CHARACTER*(*) :: OUTFILE
  INTEGER, INTENT(IN) :: NUM,M
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  INTEGER :: IERR
  NUMBER :: PSI(MPIDATA%MM,NUM)
  CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
  IF (MPIDATA%ME==0)  PRINT *, "WRITING VECTORS."
  CALL READWRITE_EIGENVECTORS(PSI,NUM,MPIDATA,M,OUTFILE,.FALSE.)
  CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
  IF (MPIDATA%ME==0)  PRINT *, "      ....VECTORS WRITTEN."
END SUBROUTINE WRITE_EIGENVECTORS

SUBROUTINE READ_EIGENVECTORS(PSI,NUM,MPIDATA,M,OUTFILE)
  USE STRUCTS
  IMPLICIT NONE
  CHARACTER*(*) :: OUTFILE
  INTEGER, INTENT(IN) :: NUM,M
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  INTEGER :: IERR
  NUMBER :: PSI(MPIDATA%MM,NUM)
  CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
  IF (MPIDATA%ME==0)  PRINT *, "READING VECTORS."
  CALL READWRITE_EIGENVECTORS(PSI,NUM,MPIDATA,M,OUTFILE,.TRUE.)
  IF (MPIDATA%ME==0)  PRINT *, "     .....VECTORS READ."
  CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
END SUBROUTINE READ_EIGENVECTORS


SUBROUTINE MYGATHERV_ONLY(HOWMANY,V1,X1,MPIDATA,M)
  USE STRUCTS
  IMPLICIT NONE !!!  in case it is moved somewhere else
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  INTEGER, INTENT(IN) :: HOWMANY,M
!! oops don't have this  NUMBER, INTENT(IN) :: V1(1:MPIDATA%M,1:MPIDATA%MB,HOWMANY) 
  NUMBER, INTENT(IN) :: V1(M,1:MPIDATA%MBLK,HOWMANY) 
  NUMBER, INTENT(OUT) :: X1(M,M,HOWMANY)
  INTEGER ::MPIERR,ii,jj

  do ii=1,howmany
     CALL MPI_GATHERV(V1(:,:,ii),MPIDATA % MM, MPIDATA % MPISIZE, X1(:,:,ii), (MPIDATA % BLOCKS)*M, &
          (MPIDATA % DISPLS)*M, MPIDATA % MPISIZE, 0, MPIDATA % COMM, MPIERR)
!! NO JUST ROOT (MEMORY)
!!     CALL MPI_BCAST(X1(:,:,ii),M,MPIDATA % MPISIZE, 0, MPIDATA % COMM, MPIERR)
     if (MPIERR.NE.0.and.MPIDATA%ME==0) THEN
        print *, "MPIERR IN MYGATHERV_ONLY",MPIERR; stop
     endif
  enddo

END SUBROUTINE MYGATHERV_ONLY


SUBROUTINE MYSCATTERV_ONLY(HOWMANY,V1,X1,MPIDATA,M)
  USE STRUCTS
  IMPLICIT NONE !!!  in case it is moved somewhere else
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  INTEGER, INTENT(IN) :: HOWMANY,M
  NUMBER, INTENT(IN) :: V1(M,1:MPIDATA%MBLK,HOWMANY) 
  NUMBER, INTENT(OUT) :: X1(M,M,HOWMANY)
  INTEGER ::MPIERR,ii,jj

  do ii=1,howmany
     CALL MPI_SCATTERV(X1(:,:,ii), (MPIDATA % BLOCKS)*M, (MPIDATA % DISPLS)*M, &
          MPIDATA % MPISIZE, V1(:,:,ii),MPIDATA % MM, MPIDATA % MPISIZE, &
          0, MPIDATA % COMM, MPIERR)
     if (MPIERR.NE.0.and.MPIDATA%ME==0) THEN
        print *, "MPIERR IN MYSCATTERV_ONLY",MPIERR; stop
     endif
  enddo

END SUBROUTINE MYSCATTERV_ONLY



SUBROUTINE READWRITE_EIGENVECTORS(PSI,NUM,MPIDATA,M,OUTFILE,READFLAG)
  USE STRUCTS
  implicit none
  ! Prints eigenvectors distributed over MPI processes to the file 
  ! specified by VECFILE
  
  LOGICAL,INTENT(IN) :: READFLAG
  CHARACTER*(*) :: OUTFILE
  INTEGER, INTENT(IN) :: NUM,M
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  NUMBER :: PSI(M,MPIDATA%MBLK,NUM)
  NUMBER, allocatable :: ALLPSI(:,:), TEMPPSI(:,:)
  INTEGER :: I, P, ME, NP, MPIERR, J,  INNUM,MB,M_IN,IVEC,NUM_IN, &
       M_CBROOT,M_IN_CBROOT,N_HERE=0,N_THERE=0,INSIZEOF
  LOGICAL :: FIXFLAG=.FALSE.

  ME = MPIDATA % ME
  NP = MPIDATA % NP
  MB = MPIDATA % MBLK

  if (ME==0) THEN
     allocate(ALLPSI(M,M))
  else
     allocate(ALLPSI(1,1))
  ENDIF
  ALLPSI(:,:)=0

  FIXFLAG=.FALSE.

  if (READFLAG) THEN
     if (me==0) then
        print *, "READING ",num," VECTORS "
        print *, "FROM FILE ---->Bin/"//TRIM(OUTFILE),"<----"
     endif
     call mpi_barrier(mpi_comm_world,MPIERR)
     
     OPEN(4,FILE = "Bin/"//TRIM(OUTFILE),FORM="UNFORMATTED",STATUS="OLD")
     READ(4) M_IN,NUM_IN,INSIZEOF

     if (M_IN.NE.M.OR.1==0) THEN
        M_CBROOT=FLOOR(REAL(M,8)**0.3333333333333333333+0.5)      
        M_IN_CBROOT=FLOOR(REAL(M_IN,8)**0.33333333333333333+0.5)  

        if (M_CBROOT**3.NE.M.OR.M_IN_CBROOT**3.NE.M_IN &
             .OR.MOD(M_CBROOT,2).NE.1.OR.MOD(M_IN_CBROOT,2).NE.1) THEN
           IF (ME==0) PRINT *, " I DON'T UNDERSTAND ",M,M_IN,M_CBROOT,M_IN_CBROOT
           STOP
        ENDIF
        N_HERE=(M_CBROOT-1)/2; N_THERE=(M_IN_CBROOT-1)/2
        
        IF (ME==0) THEN
           print *, "ON READ - LENGTHS DON'T AGREE. FIXING."
           print *, " XX: ", M,M_IN,N_HERE,N_THERE
        ENDIF
        fixflag=.true.
        if (ME==0) THEN
           allocate(TEMPPSI(M_IN,M_IN))
        ELSE
           allocate(TEMPPSI(1,1))
        ENDIF
        TEMPPSI=0
     endif


     IF ( ME==0.and.FIXFLAG.AND.INSIZEOF.NE.INT(SIZEOF(TEMPPSI)) ) THEN
        PRINT *, "BOOGA STOP 8842000 CODE BANANA.",INSIZEOF,INT(SIZEOF(TEMPPSI))
        STOP
     ENDIF
     IF (ME==0.and..NOT.FIXFLAG.AND.INSIZEOF.NE.SIZEOF(ALLPSI) ) THEN
        PRINT *, "BOOGA STOP 8842 CODE BANANA.",INSIZEOF,SIZEOF(ALLPSI)
        stop
     endif

     DO IVEC=1,NUM
        IF (ME==0) THEN
           IF (FIXFLAG) THEN
              READ(4) temppsi(:,:)
              CALL SIXD_FIX(TEMPPSI,ALLPSI,N_THERE,N_HERE)
           ELSE 
              READ(4) allpsi(:,:)
           ENDIF
        ENDIF
        CALL MYSCATTERV_ONLY(1,PSI(:,:,IVEC),ALLPSI(:,:),MPIDATA,M)
     ENDDO
     CLOSE(4)
  ELSE
     if (ME.EQ.0) THEN
        print *, "WRITING ",num," VECTORS"
        OPEN(4,FILE = "Bin/"//TRIM(OUTFILE),FORM="UNFORMATTED",STATUS="REPLACE")
        WRITE(4) M,NUM,SIZEOF(ALLPSI)
     ENDIF
     DO IVEC=1,NUM
        CALL MYGATHERV_ONLY(1,PSI(:,:,IVEC),ALLPSI(:,:),MPIDATA,M)
        if (ME.EQ.0) WRITE(4) allpsi(:,:)
     ENDDO
     IF (ME.EQ.0) CLOSE(4)
  endif


  DEALLOCATE(ALLPSI)
  IF (FIXFLAG) DEALLOCATE(TEMPPSI)

END SUBROUTINE READWRITE_EIGENVECTORS

SUBROUTINE SIXD_FIX(VEC_IN,VEC_OUT,NIN,NOUT)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: NIN,NOUT
  NUMBER,INTENT(IN) :: VEC_IN(-NIN:NIN,-NIN:NIN,-NIN:NIN,-NIN:NIN,-NIN:NIN,-NIN:NIN)
  NUMBER,INTENT(OUT) :: VEC_OUT(-NOUT:NOUT,-NOUT:NOUT,-NOUT:NOUT,-NOUT:NOUT,-NOUT:NOUT,-NOUT:NOUT)
  INTEGER :: NMIN
  
  NMIN=MIN(NIN,NOUT)
  
  VEC_OUT=0d0
  VEC_OUT(-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN)=&
       VEC_IN(-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN,-NMIN:NMIN)
END SUBROUTINE SIXD_FIX




SUBROUTINE DIAGONALIZE(PARAMS,MPIDATA,LANPAR,HAM,BLOCKSIZE)
  USE STRUCTS
  USE LANMOD
  implicit none
  TYPE(PARAMETERS), INTENT(IN) :: PARAMS
  TYPE(MPI_DATA), INTENT(IN) :: MPIDATA
  TYPE(LANPARAMS), INTENT(INOUT) :: LANPAR
  TYPE(HAMILTONIAN), INTENT(IN) :: HAM
  integer, intent(IN) :: BLOCKSIZE

  ! Call Lanczos solver
  IF(MPIDATA % ME == 0) WRITE(*,"(A)") "CALLING LANCZOS SOLVER..."
  
! CALL LANCZOS(PARAMS,LANPAR,HAM,MPIDATA)
  
  !!
CALL BLOCKLANCZOS(PARAMS,LANPAR,HAM,MPIDATA,BLOCKSIZE)
  
  ! Print out computed eigenvalues
  IF(MPIDATA % ME == 0) THEN
     WRITE(*,"(A)") "COMPUTED EIGENVALUES: "
     CALL PRINTVECTOR(LANPAR % EIGENVALUES, LANPAR % NUMEIGS)
  END IF
  
  
END SUBROUTINE DIAGONALIZE



SUBROUTINE PRINTVECTOR(X,N)
  implicit none
  ! Subroutine to print a real or complex vector to the screen
  
  INTEGER, INTENT(IN) :: N
  NUMBER, INTENT(IN) :: X(1:N)
  INTEGER :: I
  
#if ISREAL
  WRITE(*,"(F10.5)") X
#else
  DO I = 1,N
     WRITE(*,"(F20.15,F20.15)") DREAL(X(I)), DIMAG(X(I))
  END DO
#endif
  
END SUBROUTINE PRINTVECTOR

END MODULE UTILMOD

